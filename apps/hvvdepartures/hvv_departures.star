"""
Applet: HVV Departures
Summary: HVV Departures
Description: Display real-time departure times for trains, buses and ferries in Hamburg (HVV).
Author: fxb (Felix Bruns), Frank Dornberger
"""

load("encoding/json.star", "json")
load("http.star", "http")
load("images/image_akn.png", IMAGE_AKN_ASSET = "file")
load("images/image_ferry.png", IMAGE_FERRY_ASSET = "file")
load("images/image_logo.png", IMAGE_LOGO_ASSET = "file")
load("images/image_metro_bus.png", IMAGE_METRO_BUS_ASSET = "file")
load("images/image_night_bus.png", IMAGE_NIGHT_BUS_ASSET = "file")
load("images/image_s1.png", IMAGE_S1_ASSET = "file")
load("images/image_s2.png", IMAGE_S2_ASSET = "file")
load("images/image_s3.png", IMAGE_S3_ASSET = "file")
load("images/image_s5.png", IMAGE_S5_ASSET = "file")
load("images/image_s7.png", IMAGE_S7_ASSET = "file")
load("images/image_xpress_bus.png", IMAGE_XPRESS_BUS_ASSET = "file")
load("math.star", "math")
load("render.star", "render")
load("schema.star", "schema")
load("time.star", "time")

IMAGE_AKN = IMAGE_AKN_ASSET.readall()
IMAGE_FERRY = IMAGE_FERRY_ASSET.readall()
IMAGE_LOGO = IMAGE_LOGO_ASSET.readall()
IMAGE_METRO_BUS = IMAGE_METRO_BUS_ASSET.readall()
IMAGE_NIGHT_BUS = IMAGE_NIGHT_BUS_ASSET.readall()
IMAGE_S1 = IMAGE_S1_ASSET.readall()
IMAGE_S2 = IMAGE_S2_ASSET.readall()
IMAGE_S3 = IMAGE_S3_ASSET.readall()
IMAGE_S5 = IMAGE_S5_ASSET.readall()
IMAGE_S7 = IMAGE_S7_ASSET.readall()
IMAGE_XPRESS_BUS = IMAGE_XPRESS_BUS_ASSET.readall()

# The API endpoints used to retrieve locations and departures.
#
# This applet uses APIs provided by https://transitous.org/, which provide
# real-time data, without any API keys, reasonable rate-limits and for free.
#
# Please consider supporting the Transitous project:
#   https://transitous.org/
#
TRANSITOUS_API_GEOCODE_URL = "https://api.transitous.org/api/v1/geocode"
TRANSITOUS_API_STOPTIMES_URL = "https://api.transitous.org/api/v6/stoptimes"
TRANSITOUS_REQUEST_HEADERS = {
    "User-Agent": "tronbyt-hvv-departures/2.0 (+https://github.com/tronbyt/apps/tree/main/apps/hvvdepartures)",
}

# Cache API responses for a short time, as we want things to be as recent as possible.
CACHE_TTL_SECONDS = 30

# The RFC3339 date and time format string used by Go / Starlark.
RFC3339_FORMAT = "2006-01-02T15:04:05Z07:00"

# The default station ID to use, if none is set by the user.
# This currently defaults to "Hamburg Hbf" (main station).
DEFAULT_STATION_ID = "de-DELFI_de:02000:10950_G"

# Configuration for backgrounds and colors for all known lines.
LINE_CONFIG = {
    # Subways (U-Bahn)
    "u1": {"background-color": "#0072bc", "color": "#ffffff"},
    "u2": {"background-color": "#ed1c24", "color": "#ffffff"},
    "u3": {"background-color": "#ffde00", "color": "#2f2f2f"},
    "u4": {"background-color": "#00aaad", "color": "#ffffff"},

    # Suburban trains (S-Bahn)
    "s1": {"image": IMAGE_S1, "color": "#ffffff"},
    "s2": {"image": IMAGE_S2, "color": "#ffffff"},
    "s3": {"image": IMAGE_S3, "color": "#ffffff"},
    "s5": {"image": IMAGE_S5, "color": "#ffffff"},
    "s7": {"image": IMAGE_S7, "color": "#ffffff"},

    # AKN commuter trains
    "a1": {"image": IMAGE_AKN, "color": "#ffffff"},
    "a2": {"image": IMAGE_AKN, "color": "#ffffff"},
    "a3": {"image": IMAGE_AKN, "color": "#ffffff"},

    # Buses (MetroBus, XpressBus, NachtBus)
    "metro_bus": {"image": IMAGE_METRO_BUS, "color": "#ffffff"},
    "xpress_bus": {"image": IMAGE_XPRESS_BUS, "color": "#ffffff"},
    "night_bus": {"image": IMAGE_NIGHT_BUS, "color": "#ffffff"},

    # Regional trains (Regional-Bahn, Regional-Express)
    "rb": {"background-color": "#2f2f2f", "color": "#ffffff"},
    "re": {"background-color": "#2f2f2f", "color": "#ffffff"},
}

# These are used as fallbacks in case there is no specific config above.
# Basically this will result in only rendering the plain line name, without
# any background image or color.
DEFAULT_SUBWAY_CONFIG = {"background-color": None, "color": "#ffffff"}
DEFAULT_SUBURBAN_CONFIG = {"image": None, "color": "#ffffff"}
DEFAULT_BUS_CONFIG = {"image": None, "color": "#ffffff"}
DEFAULT_REGIONAL_TRAIN_CONFIG = {"background-color": None, "color": "#ffffff"}

# Other colors used throughout the applet.
COLOR_BACKGROUND = "#000000"
COLOR_SEPARATOR = "#1f1f1f"
COLOR_MESSAGE_INFO = "#ffffff"
COLOR_MESSAGE_ERROR = "#ff9900"
COLOR_DEPARTURE_TIME = "#ff9900"
COLOR_DEPARTURE_TIME_DELAYED = "#ff0000"
COLOR_DEPARTURE_TIME_ON_TIME = "#00ff00"

def render_subway_icon(id, name):
    """Render a rectangular subway (U-Bahn) icon.

    Args:
        id: The id of the subway line.
        name: The name of the subway line.

    Returns:
        A definition of what to render.
    """
    data = LINE_CONFIG.get(id, DEFAULT_SUBWAY_CONFIG)
    background_color = data["background-color"]
    color = data["color"]
    return render.Box(width = 18, height = 15, padding = 2, child = render.Stack(children = [
        render.Box(width = 14, height = 9, color = background_color) if background_color != None else None,
        render.Box(width = 16, height = 9, child = render.Text(name, offset = -1, font = "tom-thumb", color = color)),
    ]))

def render_suburban_icon(id, name):
    """Render a pill shaped suburban train (S-Bahn) icon.

    Args:
        id: The id of the suburban train line.
        name: The name of the suburban train line.

    Returns:
        A definition of what to render.
    """
    data = LINE_CONFIG.get(id, DEFAULT_SUBURBAN_CONFIG)
    image = data["image"]
    color = data["color"]
    return render.Box(width = 18, height = 15, padding = 2, child = render.Stack(children = [
        render.Image(width = 14, height = 9, src = image) if image != None else None,
        render.Box(width = 16, height = 9, child = render.Text(name, offset = -1, font = "tom-thumb", color = color)),
    ]))

def render_bus_icon(id, name):
    """Render a diamond like bus (MetroBus, XpressBus or NachtBus) icon.

    Args:
        id: The id of the bus line.
        name: The name of the bus line.

    Returns:
        A definition of what to render.
    """
    is_xpress_bus = id[0] == "x"
    is_night_bus = len(id) == 3 and id[0] == "6"
    data = LINE_CONFIG.get("xpress_bus" if is_xpress_bus else "night_bus" if is_night_bus else "metro_bus", DEFAULT_BUS_CONFIG)
    image = data["image"]
    color = data["color"]
    expand = len(name) > 3
    return render.Box(width = 18, height = 15, padding = 0 if expand else 1, child = render.Stack(children = [
        render.Image(width = 18 if expand else 16, height = 9, src = image) if image != None else None,
        render.Box(width = 20 if expand else 18, height = 9, child = render.Text(name, offset = -1, font = "tom-thumb", color = color)),
    ]))

def render_regional_train_icon(id, name):
    """Render a rectangular regional (express) train (Regional-Bahn, Regional-Express) icon.

    Args:
        id: The id of the regional train line.
        name: The name of the regional train line.

    Returns:
        A definition of what to render.
    """
    data = LINE_CONFIG.get(id[0:2], DEFAULT_REGIONAL_TRAIN_CONFIG)
    background_color = data["background-color"]
    color = data["color"]
    return render.Box(width = 18, height = 15, padding = 1, child = render.Stack(children = [
        render.Box(width = 15, height = 13, color = background_color),
        render.Column(children = [
            render.Box(height = 6, child = render.Text(name[0:2], offset = -1, font = "tom-thumb", color = color)),
            render.Box(height = 6, child = render.Text(name[2:], offset = -1, font = "tom-thumb", color = color)),
        ]),
    ]))

def render_ferry_icon(name):
    """Render a trapezoid shaped ferry icon.

    Args:
        name: The name (line number) of the ferry line.

    Returns:
        A definition of what to render.
    """
    return render.Box(width = 18, height = 15, padding = 2, child = render.Stack(children = [
        render.Image(width = 14, height = 9, src = IMAGE_FERRY),
        render.Box(width = 16, height = 9, child = render.Text(name, offset = -1, font = "tom-thumb", color = "#ffffff")),
    ]))

def render_line_icon(line):
    """Render an icon for a given line.

    Args:
        line: A "line" dictionary, built from the Transitous API response.

    Returns:
        A definition of what to render.
    """
    id = line["id"]
    name = line["name"]
    product = line["product"]

    if product == "subway":
        return render_subway_icon(id, name)
    elif product == "suburban" or \
         product == "akn":
        return render_suburban_icon(id, name)
    elif product == "regional-train" or \
         product == "regional-express-train":
        return render_regional_train_icon(id, name)
    elif product == "bus" or \
         product == "express-bus":
        return render_bus_icon(id, name)
    elif product == "ferry":
        return render_ferry_icon(name)

    # Fallback for anything we don't have a dedicated icon for (e.g. taxi or
    # long distance trains/buses): just an empty box.
    return render.Box(width = 18, height = 15)

def render_relative_departure_time(time_actual):
    """Render a relative departure time.

    Args:
        time_actual: The actual departure time.

    Returns:
        A definition of what to render.
    """
    diff_minutes = math.floor((time_actual - time.now()).minutes)

    return render.Text(
        content = "now" if diff_minutes <= 0 else ("%s min" % diff_minutes),
        height = 7,
        font = "tb-8",
        color = COLOR_DEPARTURE_TIME,
    )

def render_absolute_departure_time(format, time_planned, time_actual):
    """Render an absolute departure time, including a delay indicator.

    Args:
        format: The time layout string to use.
        time_planned: The planned departure time.
        time_actual: The actual departure time.

    Returns:
        A definition of what to render.
    """
    delay_minutes = math.floor((time_actual - time_planned).minutes)

    return render.Row(children = [
        render.Text(
            content = time_planned.format(format),
            height = 7,
            font = "tb-8",
            color = COLOR_DEPARTURE_TIME,
        ),
        render.Text(
            content = "+%d" % delay_minutes,
            height = 7,
            font = "tom-thumb",
            color = COLOR_DEPARTURE_TIME_DELAYED if delay_minutes > 0 else COLOR_DEPARTURE_TIME_ON_TIME,
        ),
    ])

def render_departure_time(time_format, time_planned, time_actual):
    """Render a relative or an absolute departure time.

    Args:
        time_format: The time layout string to use or "relative".
        time_planned: The planned departure time.
        time_actual: The actual departure time.

    Returns:
        A definition of what to render.
    """
    if time_format == "relative":
        return render_relative_departure_time(time_actual)
    else:
        return render_absolute_departure_time(time_format, time_planned, time_actual)

def render_departure(departure, time_format):
    """Render which line, including icon, departs at what time.

    Args:
        departure: A "departure" dictionary, retrieved from the API.
        time_format: The time layout string to use or "relative".

    Returns:
        A definition of what to render.
    """
    time_planned = time.parse_time(departure["plannedWhen"])
    time_actual = time.parse_time(departure["when"])

    return render.Row(
        expanded = True,
        main_align = "start",
        cross_align = "center",
        children = [
            render.Box(
                width = 18,
                height = 15,
                child = render_line_icon(departure["line"]),
            ),
            render.Column(
                children = [
                    render.Marquee(
                        width = 48,
                        child = render.Text(
                            content = departure["direction"],
                            height = 8,
                            font = "tb-8",
                        ),
                    ),
                    render.Marquee(
                        render_departure_time(time_format, time_planned, time_actual),
                        width = 48,
                    ),
                ],
            ),
        ],
    )

# Maps a "category" (see 'classify_stop_time') to the "product" string that
# 'render_line_icon' expects.
CATEGORY_TO_PRODUCT = {
    "subway": "subway",
    "suburban": "suburban",
    "akn": "akn",
    "rb": "regional-train",
    "re": "regional-express-train",
    "bus": "bus",
    "express_bus": "express-bus",
    "ferry": "ferry",
}

# Maps a "category" (see 'classify_stop_time') to the Transitous API's
# "mode" enum value used to request it.
CATEGORY_TO_API_MODE = {
    "subway": "SUBWAY",
    "suburban": "SUBURBAN",
    "akn": "REGIONAL_RAIL",
    "rb": "REGIONAL_RAIL",
    "re": "REGIONAL_RAIL",
    "bus": "BUS",
    "express_bus": "BUS",
    "ferry": "FERRY",
}

def classify_stop_time(stop_time):
    """Classify a Transitous 'stopTimes' entry into one of our categories.

    AKN and RB/RE all share the "REGIONAL_RAIL" mode, and regular and Xpress
    buses both share the "BUS" mode, so those are further distinguished by
    the line's (lowercased) short name.

    Args:
        stop_time: A single entry from the Transitous 'stoptimes' response.

    Returns:
        A tuple of (category, id, name). "category" is None if this stop
        time doesn't map to any product we know how to render (e.g. a
        long-distance train passing through a station we queried).
    """
    mode = stop_time.get("mode")
    name = stop_time.get("routeShortName") or stop_time.get("displayName") or ""
    id = name.lower()

    if mode == "SUBWAY":
        return ("subway", id, name)
    elif mode == "SUBURBAN":
        return ("suburban", id, name)
    elif mode == "FERRY":
        return ("ferry", id, name)
    elif mode == "BUS":
        is_xpress_bus = len(id) > 0 and id[0] == "x"
        return ("express_bus" if is_xpress_bus else "bus", id, name)
    elif mode == "REGIONAL_RAIL":
        is_akn = len(id) > 1 and id[0] == "a" and id[1] in "0123456789"
        if is_akn:
            return ("akn", id, name)
        elif id.startswith("re"):
            return ("re", id, name)
        elif id.startswith("rb"):
            return ("rb", id, name)

    # Fallback for anything we don't recognize (e.g. long-distance trains).
    return (None, id, name)

def fetch_departures(station_id, categories, at_time = None, max_results = 2):
    """Fetch departures given a station identifier.

    Args:
        station_id: A Transitous stop identifier to fetch departures for.
        categories: The set of categories (see 'classify_stop_time') to
            include in the result.
        at_time: Optional RFC3339 timestamp to fetch departures from,
            instead of the current time.
        max_results: Return at most this number of results.

    Returns:
        A list of "departure" dictionaries (in the shape 'render_departure'
        expects), or None if the request failed.
    """
    if len(categories) == 0:
        return []

    # Only ask the API for the modes we actually need, then further filter
    # and classify results client-side (see 'classify_stop_time').
    modes = {}
    for category in categories:
        modes[CATEGORY_TO_API_MODE[category]] = True

    # Overfetch, since results get filtered and classified client-side below.
    params = {
        "stopId": station_id,
        "n": str(max_results * 15),
        "mode": ",".join(modes.keys()),
    }

    if at_time != None:
        params["time"] = at_time

    response = http.get(
        url = TRANSITOUS_API_STOPTIMES_URL,
        params = params,
        headers = TRANSITOUS_REQUEST_HEADERS,
        ttl_seconds = CACHE_TTL_SECONDS,
    )

    if response.status_code != 200:
        print("API request failed with status %d" % response.status_code)
        return None

    data = response.json()

    departures = []
    for stop_time in data.get("stopTimes", []):
        (category, id, name) = classify_stop_time(stop_time)
        if category == None or category not in categories:
            continue

        place = stop_time.get("place", {})
        departures.append({
            "line": {
                "id": id,
                "name": name,
                "product": CATEGORY_TO_PRODUCT[category],
            },
            "direction": stop_time.get("headsign", ""),
            "plannedWhen": place.get("scheduledDeparture"),
            "when": place.get("departure"),
        })

        if len(departures) >= max_results:
            break

    return departures

def get_config_option_value(config, key, default = None):
    """Get the value of a 'schema.Option' from the applet configuration.

    Args:
        config: The applet configuration.
        key: The configuration key.
        default: The default value to fallback to.

    Returns:
        The value of the 'schema.Option' or the fallback value.
    """
    blob = config.str(key)
    data = json.decode(blob) if blob != None else None
    return data["value"] if data != None else default

def is_legacy_station_id(station_id):
    """Check whether a station ID is in HVV's old numeric format.

    Args:
        station_id: The configured station ID.

    Returns:
        True if this looks like an old HVV station ID (e.g. "90"), which
        can't be resolved against the Transitous API.
    """
    return station_id != None and len(station_id) > 0 and station_id.isdigit()

def parse_config(config):
    """Parse the applet configuration into some convenient structures.

    Args:
        config: The applet configuration.

    Returns:
        A tuple of transformed applet configuration values.
    """
    station_id = get_config_option_value(config, "station_id", DEFAULT_STATION_ID)
    time_format = config.str("time_format", "relative")
    time_offset = time.parse_duration(config.str("time_offset", "0m"))

    # Which means of transport are selected? Each maps to one of the
    # categories used in 'classify_stop_time' / 'fetch_departures'.
    include_subway = config.bool("include_subway", True)
    include_suburban = config.bool("include_suburban", True)
    include_bus = config.bool("include_bus", True)
    include_express_bus = config.bool("include_express_bus", True)
    include_rb = config.bool("include_rb", True)
    include_re = config.bool("include_re", True)
    include_akn = config.bool("include_akn", True)
    include_ferry = config.bool("include_ferry", True)
    is_anything_selected = include_subway or include_suburban or \
                           include_bus or include_express_bus or \
                           include_rb or include_re or \
                           include_akn or include_ferry

    categories = []
    if include_subway:
        categories.append("subway")
    if include_suburban:
        categories.append("suburban")
    if include_bus:
        categories.append("bus")
    if include_express_bus:
        categories.append("express_bus")
    if include_akn:
        categories.append("akn")
    if include_rb:
        categories.append("rb")
    if include_re:
        categories.append("re")
    if include_ferry:
        categories.append("ferry")

    return (station_id, time_format, time_offset, is_anything_selected, categories)

def render_message(message, color):
    """Render a message in a given color, below the HVV logo.

    Args:
        message: The message to show.
        color: The message color to use.

    Returns:
        A definition of what to render.
    """
    return render.Root(
        child = render.Box(
            color = COLOR_BACKGROUND,
            child = render.Column(
                children = [
                    render.Box(height = 16, child = render.Image(IMAGE_LOGO)),
                    render.Box(height = 16, child = render.WrappedText(
                        content = message,
                        font = "tom-thumb",
                        color = color,
                    )),
                ],
            ),
        ),
    )

def main(config):
    """The applet entry point.

    Args:
        config: The applet configuration.

    Returns:
        A definition of what to render.
    """
    (station_id, time_format, time_offset, is_anything_selected, categories) = parse_config(config)

    # None of the products are selected...
    if is_anything_selected == False:
        return render_message("Choose at least one vessel", COLOR_MESSAGE_INFO)

    if is_legacy_station_id(station_id):
        return render_message("Please re-select your station in the app settings", COLOR_MESSAGE_INFO)

    at_time = (time.now() + time_offset).format(RFC3339_FORMAT) if time_offset != 0 else None

    # Fetch departures and show an error message, if it fails.
    departures = fetch_departures(station_id, categories, at_time)
    if departures == None:
        return render_message("Error fetching departures!", COLOR_MESSAGE_ERROR)

    # Slice departures to a maximum of two, although
    # it is already specified in the API request.
    departures = departures[0:2]

    # No departures were found...
    if len(departures) == 0:
        return render_message("Couldn't find any departures", COLOR_MESSAGE_INFO)

    return render.Root(
        child = render.Box(
            color = COLOR_BACKGROUND,
            child = render.Column(
                expanded = True,
                children = [
                    render_departure(departures[0], time_format) if len(departures) > 0 else None,
                    render.Box(width = 64, height = 1, color = COLOR_SEPARATOR),
                    render_departure(departures[1], time_format) if len(departures) > 1 else None,
                ],
            ),
        ),
    )

def find_stations(query, max_results = 2):
    """Search the API for a list of stations matching a (fuzzy) query.

    Args:
        query: The (fuzzy) query string.
        max_results: Return at most this number of results.

    Returns:
        A list of 'schema.Option', each corresponding to a station.
    """
    query = query.strip(" ")
    if len(query) == 0:
        return []

    response = http.get(
        url = TRANSITOUS_API_GEOCODE_URL,
        params = {
            "text": query,
            "type": "STOP",
            "numResults": str(max_results),
        },
        headers = TRANSITOUS_REQUEST_HEADERS,
    )

    if response.status_code != 200:
        print("API request failed with status %d" % response.status_code)
        return []

    data = response.json()

    return [schema.Option(display = station["name"], value = station["id"]) for station in data]

def get_schema():
    time_format_options = [
        schema.Option(
            display = "Relative",
            value = "relative",
        ),
        schema.Option(
            display = "Absolute (24h)",
            value = "15:04",
        ),
        schema.Option(
            display = "Absolute (12h)",
            value = "3:04 PM",
        ),
    ]

    time_offset_options = [
        schema.Option(
            display = "now",
            value = "0m",
        ),
        schema.Option(
            display = "in 5 minutes",
            value = "5m",
        ),
        schema.Option(
            display = "in 10 minutes",
            value = "10m",
        ),
        schema.Option(
            display = "in 15 minutes",
            value = "15m",
        ),
    ]

    return schema.Schema(
        version = "1",
        fields = [
            schema.Typeahead(
                id = "station_id",
                name = "Station",
                desc = "Pick a station",
                icon = "mapPin",
                handler = find_stations,
            ),
            schema.Dropdown(
                id = "time_format",
                name = "Time format",
                desc = "Pick a time format",
                icon = "clock",
                default = time_format_options[0].value,
                options = time_format_options,
            ),
            schema.Dropdown(
                id = "time_offset",
                name = "Time offset",
                desc = "Pick a time offset",
                icon = "plus",
                default = time_offset_options[0].value,
                options = time_offset_options,
            ),
            schema.Toggle(
                id = "include_subway",
                name = "U-Bahn",
                desc = "Include subways",
                icon = "trainSubway",
                default = True,
            ),
            schema.Toggle(
                id = "include_suburban",
                name = "S-Bahn",
                desc = "Include suburban trains",
                icon = "train",
                default = True,
            ),
            schema.Toggle(
                id = "include_bus",
                name = "MetroBus",
                desc = "Include buses",
                icon = "bus",
                default = True,
            ),
            schema.Toggle(
                id = "include_express_bus",
                name = "XpressBus",
                desc = "Include express buses",
                icon = "bus",
                default = True,
            ),
            schema.Toggle(
                id = "include_akn",
                name = "AKN",
                desc = "Include AKN commuter trains",
                icon = "train",
                default = True,
            ),
            schema.Toggle(
                id = "include_rb",
                name = "RB",
                desc = "Include regional trains",
                icon = "train",
                default = True,
            ),
            schema.Toggle(
                id = "include_re",
                name = "RE",
                desc = "Include regional express trains",
                icon = "train",
                default = True,
            ),
            schema.Toggle(
                id = "include_ferry",
                name = "Ferry",
                desc = "Include ferrys",
                icon = "ship",
                default = True,
            ),
        ],
    )
